enum ArtifactType {
    jar,
    pom
}

def exists(String path) {
    String exists = sh script: "[ -f ${path} ] && echo 'TRUE' || echo 'FALSE'", returnStdout: true
    return exists.trim() == 'TRUE'
}

static def createArtifact(String id, ArtifactType type, String path) {
    return [artifactId: id, classifier: '', type: type.name(), file: path]
}

pipeline {
    agent any
    tools {
        maven 'Maven 3.9.11'
        jdk 'GVM 25'
    }
    environment {
        // maven
        LOCAL_REPOSITORY_LOCATION = '/var/jenkins_home/.m2/repository'
        MAVEN_ARG_MAVEN_REPO_LOCAL = "-Dmaven.repo.local=${LOCAL_REPOSITORY_LOCATION}"
        MAVEN_ARG_EXPRESSION_GROUP_ID = "-Dexpression=project.groupId"
        MAVEN_ARG_EXPRESSION_VERSION = "-Dexpression=project.version"
        // nexus
        NEXUS_VERSION = 'nexus3'
        NEXUS_PROTOCOL = 'https'
        NEXUS_URL = 'repository.nerosec.com'
        NEXUS_REPOSITORY = 'maven-nexus-repo'
        NEXUS_CREDENTIALS_ID = 'nexus-user'
    }
    stages {
        stage('GIT CLONE') {
            steps {
                git url: 'https://github.com/emilancius/noxx.git', branch: 'master'
            }
        }
        stage('BUILD') {
            steps {
                sh "mvn ${MAVEN_ARG_MAVEN_REPO_LOCAL} -DskipTests clean install"
            }
        }
        stage('TEST') {
            steps {
                sh "mvn ${MAVEN_ARG_MAVEN_REPO_LOCAL} test"
            }
        }
        stage('PUBLISH') {
            steps {
                script {
                    def directory = "${env.WORKSPACE}"
                    def names = ['noxx-commons']
                    def groupId = sh script: "mvn help:evaluate ${MAVEN_ARG_MAVEN_REPO_LOCAL} ${MAVEN_ARG_EXPRESSION_GROUP_ID} -q -DforceStdout", returnStdout: true
                    def version = sh script: "mvn help:evaluate ${MAVEN_ARG_MAVEN_REPO_LOCAL} ${MAVEN_ARG_EXPRESSION_VERSION} -q -DforceStdout", returnStdout: true
                    def artifacts = []
                    names.each { name ->
                        def path = "${directory}/${name}"
                        def jar = "${path}/target/${name}-${version}.jar"
                        if (exists(jar)) {
                            artifacts << createArtifact(name, ArtifactType.jar, jar)
                            artifacts << createArtifact(name, ArtifactType.pom, "${path}/pom.xml")
                        }
                    }
                    if (!artifacts.isEmpty()) {
                        nexusArtifactUploader(
                                nexusVersion: NEXUS_VERSION,
                                protocol: NEXUS_PROTOCOL,
                                nexusUrl: NEXUS_URL,
                                credentialsId: NEXUS_CREDENTIALS_ID,
                                repository: NEXUS_REPOSITORY,
                                groupId: groupId,
                                version: version,
                                artifacts: artifacts
                        )
                    }
                }
            }
        }
    }
}